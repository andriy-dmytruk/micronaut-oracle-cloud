plugins {
    id 'io.micronaut.build.internal.oraclecloud-module'
}

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    implementation("io.netty:netty-codec-http")
    api("com.oracle.oci.sdk:oci-java-sdk-common-httpclient:$oci3Version")
    api mn.micronaut.serde.api
    implementation mn.micronaut.serde.support

    // processor
//    annotationProcessor projects.oraclecloudHttpclientNettyProcessor
    api projects.oraclecloudCommon, {
        exclude group:'commons-logging', module:'commons-logging'
    }
    api("com.oracle.oci.sdk:oci-java-sdk-addons-graalvm:$oci3Version") {
        transitive = false
    }

    testImplementation("io.micronaut:micronaut-http-server-netty")
    // for self-signed certs
    testImplementation("org.bouncycastle:bcpkix-jdk15on:1.70")
    testImplementation(projects.oraclecloudSdk) {
        exclude group: 'com.oracle.oci.sdk'
    }
//    testImplementation "com.oracle.oci.sdk:oci-java-sdk-monitoring:$oci3Version"

    annotationProcessor mn.micronaut.inject.java
    implementation mn.micronaut.inject.java
    annotationProcessor mn.micronaut.serde.processor
    implementation mn.micronaut.serde.processor
    annotationProcessor project(":oraclecloud-httpclient-netty-processor")
    implementation project(":oraclecloud-httpclient-netty-processor")

    testImplementation mn.micronaut.inject.java.test
    testImplementation mn.micronaut.serde.api
    testImplementation mn.micronaut.serde.jackson
    testImplementation mn.micronaut.management

    for (name in gradle.oci3Bom.dependencyManagement.dependencies.dependency.artifactId*.text()) {
        if (name == "oci-java-sdk-shaded-full") {
            continue
        }
        compileOnly "com.oracle.oci.sdk:$name:$oci3Version"
        testImplementation "com.oracle.oci.sdk:$name:$oci3Version"
        annotationProcessor "com.oracle.oci.sdk:$name:$oci3Version"
    }
}

micronautBuild {
    binaryCompatibility.enabled = false
}

configurations.all {
    resolutionStrategy {
        eachDependency { DependencyResolveDetails details ->
            if (details.requested.group == 'io.micronaut.serde') {
                details.useVersion "1.5.2-SNAPSHOT"
            } else if (details.requested.name == 'micronaut-inject') {
                details.useVersion "3.9.0-SNAPSHOT"
            }
        }
    }
}
